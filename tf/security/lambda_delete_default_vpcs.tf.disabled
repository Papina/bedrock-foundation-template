
#====================================================================================================
# Description : Delete default VPCs resources (subnets/sg/rt/nacls) in all accounts and all regions
# Author      : Benjamin Luton
# Date        : 2021-08-02
# Version     : 1.0.0
#====================================================================================================

# -- Function logging
#--------------------------------------------------------------------------------------------------
resource "aws_cloudwatch_log_group" "delete_default_vpcs_lambda_log_group" {
  name              = "/aws/lambda/${aws_lambda_function.delete_default_vpcs_function.function_name}"
  retention_in_days = 3
}

resource "aws_iam_role" "delete_default_vpcs_role" {
  name                 = "bedrock-delete-default-vpcs-role"
  max_session_duration = 43200
  managed_policy_arns  = ["arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"]
  assume_role_policy   = data.aws_iam_policy_document.event_lambda.json
  inline_policy {
    name   = "OperationalAssumeMember"
    policy = data.aws_iam_policy_document.cross_account.json
  }
  inline_policy {
    name   = "DeleteDefaultVPCs"
    policy = data.aws_iam_policy_document.deldefvpc.json
  }
  inline_policy {
    name   = "SNSPublish"
    policy = data.aws_iam_policy_document.publish_sns.json
  }
  tags = var.tags
}

# -- Create Function
#--------------------------------------------------------------------------------------------------
data "archive_file" "delete_default_vpcs_lambda_zip" {
  type        = "zip"
  source_file = "./lambda/delete-default-vpc.py"
  output_path = "./lambda/delete-default-vpc.zip"
}

resource "aws_lambda_function" "delete_default_vpcs_function" {
  function_name = "bedrock-Delete-Default-VPCs"
  description   = "Remove default VPC"
  dead_letter_config {
    target_arn = join(":", [
      "arn:aws:sns",
      local.base_region,
      lookup(data.terraform_remote_state.org.outputs.acc, "Audit"),
      "bedrock-audit-deadletter"
    ])
  }
  handler     = "delete-default-vpc.handler"
  runtime     = "python3.8"
  memory_size = 256
  timeout     = 900
  role        = aws_iam_role.delete_default_vpcs_role.arn
  environment {
    variables = {
      REGIONS    = join(",", data.aws_regions.current.names)
      MEMBERACCT = join(",", (data.terraform_remote_state.org.outputs.all_accounts_ids))
      MEMBERROLE = var.crossaccountrole.lambda
    }
  }
  tags     = var.tags
  filename = "./lambda/delete-default-vpc.zip"
}



